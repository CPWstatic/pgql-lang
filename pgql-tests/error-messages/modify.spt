module update

language pgql-lang

test Unresolved variable in SELECT [[

  SELECT ( [[MODIFY UPDATE x SET PROPERTY x.prop = 3]] FROM g MATCH (x) ) AS v
    FROM g MATCH(n)

]] error like "SELECT clause expected here" at #1

test Unresolved variable in SELECT [[

  SELECT n AS n
    FROM g MATCH (n)
   WHERE EXISTS ( [[MODIFY g UPDATE x SET PROPERTY x.prop = 3]] FROM g MATCH(x) )

]] error like "SELECT clause expected here" at #1

test Set a property that is grouped by [[

    MODIFY g UPDATE n SET PROPERTY [[n.prop]] = 123
      FROM g MATCH (n)
  GROUP BY n.prop AS nProp

]] error like "Cannot set a property that is grouped by" at #1

test Update property value to vertex or edge (1) [[

  MODIFY g UPDATE n SET PROPERTIES n.prop1 = [[n]]
                                 , n.prop2 = [[m]]
                                 , n.prop3 = [[e]]
    FROM g MATCH (n) -[e]-> (m)

]] error like "Cannot set the value of a property to a vertex or an edge" at #1, #2, #3

test Update property value to vertex or edge (2) [[

  MODIFY g UPDATE n SET PROPERTY n.prop = ( SELECT [[x AS x]] FROM g MATCH (x) LIMIT 1 )
    FROM g MATCH (n) -[e]-> (m)

]] error like "Scalar subquery not allowed to return a vertex or an edge" at #1

test Update property value to vertex or edge (3) [[

  MODIFY g UPDATE n SET PROPERTY n.prop = [[n]]
    FROM g MATCH (n)
  GROUP BY n

]] error like "Cannot set the value of a property to a vertex or an edge" at #1

test Insert element with property value of type vertex or edge (1) [[

  MODIFY g INSERT VERTEX m PROPERTIES n.prop1 = [[n]]
                                    , n.prop2 = [[m]]
                                    , n.prop3 = [[e]]
    FROM g MATCH (n) -[e]-> (m)

]] error like "Cannot set the value of a property to a vertex or an edge" at #1, #2, #3

test Insert element with property value of type vertex or edge (2) [[

  MODIFY g INSERT VERTEX m PROPERTY m.prop = ( SELECT [[x AS x]] FROM g MATCH (x) LIMIT 1 )
    FROM g MATCH (n) -[e]-> (m)

]] error like "Scalar subquery not allowed to return a vertex or an edge" at #1

test Insert element with property value of type vertex or edge (3) [[

  MODIFY g INSERT VERTEX m PROPERTY m.prop = [[n]]
    FROM g MATCH (n)
  GROUP BY n

]] error like "Cannot set the value of a property to a vertex or an edge" at #1


test Set a property multiple times in UPDATE [[

  MODIFY g UPDATE n SET PROPERTIES [[n.prop]] = 1, [[n.prop]] = 2
    FROM g MATCH (n)

]] error like "Property is set multiple times" at #1, #2

test Set a property multiple times in INSERT [[

  MODIFY g INSERT VERTEX m PROPERTIES [[m.prop]] = 1, [[m.prop]] = 2
    FROM g MATCH (n)

]] error like "Property is set multiple times" at #1, #2
