module Modify

imports

  Expressions
  Names

template options

  keyword -/- [a-zA-Z0-9]

context-free syntax

  ModifyClause.ModifyClause = Modification+ {case-insensitive}

  Modification = InsertClause
  Modification = UpdateClause
  Modification = DeleteClause

  InsertClause.InsertClause = <INSERT <IntoClause?> <{Insertion ", "}+>> {case-insensitive}

  IntoClause.IntoClause = <INTO <GraphName>> {case-insensitive}

  Insertion.VertexInsertion = <VERTEX <Identifier> <LabelsSpecification?> <PropertiesSpecification?>> {case-insensitive}
  Insertion.DirectedEdgeInsertion = <EDGE <Identifier> BETWEEN <VarRef> AND <VarRef> <LabelsSpecification?> <PropertiesSpecification?>> {case-insensitive}

  LabelsSpecification.Labels = <LABELS ( <{Identifier ", "}+> )> {case-insensitive}
  PropertiesSpecification.Properties = <PROPERTIES ( <{SetProperty ", "}+> )> {case-insensitive}
  SetProperty.SetProperty = <<PropRef> = <Exp>>

  UpdateClause.UpdateClause = <UPDATE <{Update ", "}+>> {case-insensitive}
  Update.Update = <<VarRef> <SetProperties>>
  SetProperties.SetProperties = <SET PROPERTIES ( <{SetProperty ", "}+> )> {case-insensitive}

  DeleteClause.DeleteClause = <DELETE <{VarRef ", "}+>> {case-insensitive}
