module Modify

imports

  Expressions
  Names

template options

  keyword -/- [a-zA-Z0-9]

context-free syntax

  ModifyClause.ModifyClause = <MODIFY <Identifier?> ( <Modification+> )> {case-insensitive}

  Modification = InsertClause
  Modification = UpdateClause
  Modification = DeleteClause

  InsertClause.InsertClause = <INSERT <{Insertion ", "}+>> {case-insensitive}

  Insertion.VertexInsertion = <VERTEX <Identifier> <LabelsSpecification?> <PropertiesSpecification?>> {case-insensitive}
  Insertion.DirectedEdgeInsertion = <<Directed?> EDGE <Identifier> BETWEEN <VarRef> AND <VarRef> <LabelsSpecification?> <PropertiesSpecification?>> {case-insensitive}
  Directed.Directed = <DIRECTED> {case-insensitive}

  LabelsSpecification.Labels = <LABELS ( <{Exp ", "}+> )> {case-insensitive}
  PropertiesSpecification.Properties = <PROPERTIES ( <{SetProperty ", "}+> )> {case-insensitive}
  SetProperty.SetProperty = <<PropRef> = <Exp>>

  UpdateClause.UpdateClause = <UPDATE <{Update ", "}+>> {case-insensitive}
  Update.Update = <<VarRef> <SetProperties>>
  SetProperties.SetProperties = <SET PROPERTIES ( <{SetProperty ", "}+> )> {case-insensitive}

  DeleteClause.DeleteClause = <DELETE <{VarRef ", "}+>> {case-insensitive}
