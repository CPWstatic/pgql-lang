module Modify

imports

  Expressions
  Names

template options

  keyword -/- [a-zA-Z0-9]

context-free syntax

  ModifyClause.ModifyClause = <MODIFY <Identifier?> <Modification+>> {case-insensitive}

  Modification = Insertion
  Modification = Deletion
  Modification = Update

  Insertion.VertexInsertion = <INSERT VERTEX <Identifier> <LabelsSpecification?> <PropertiesSpecification?>> {case-insensitive}
  Insertion.DirectedEdgeInsertion = <INSERT <Directed?> EDGE <Identifier> BETWEEN <VarRef> AND <VarRef> <LabelsSpecification?> <PropertiesSpecification?>> {case-insensitive}
  Directed.Directed = <DIRECTED> {case-insensitive}
  LabelsSpecification.Labels = <<LabelsKeyword> <{Identifier ", "}+>>
  PropertiesSpecification.Properties = <<PropertiesKeyword> <{SetPropertyExpression ", "}+>>
  SetPropertyExpression.Set = <<PropRef> = <Exp>>

  LabelsKeyword.Labels = <LABELS> {case-insensitive}
  LabelsKeyword.Label = <LABEL> {case-insensitive}
  PropertiesKeyword.Properties = <PROPERTIES> {case-insensitive}
  PropertiesKeyword.Property = <PROPERTY> {case-insensitive}

  Update.Update = <UPDATE <{VarRef ", "}+> <SetProperties>> {case-insensitive}
  SetProperties.SetProperties = <SET <PropertiesKeyword> <{SetProperty ", "}+>> {case-insensitive}
  SetProperty.SetProperty = <<PropRef> = <Exp>>
  Deletion.Deletion = <DELETE <{VarRef ", "}+>> {case-insensitive}
