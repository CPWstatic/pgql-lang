module pgql-lang

imports

  Layout
  Names
  Literals
  GraphPattern
  SolutionModifiers
  Legacy
  Expressions
  Experimental

context-free start-symbols

  Start

template options

  keyword -/- [a-zA-Z0-9]

context-free syntax

  Start = Query

  Query.Pgql11Query =
    <
      <{CommonPathExpression "\n"}*>
      <SelectOrUpdateClause>
      <Pgql11FromClause?>
      <Pgql11MatchClause>
      <WhereClause?>
      <GroupByClause?>
      <HavingClause?>
      <OrderByClause?>
      <LimitOffsetClauses?>
    >

  Query = Legacy10Query
  Query = ExperimentalQuery

  CommonPathExpression.CommonPathExpression = <PATH <REGULAR-IDENTIFIER> <AsignSymbol> <PathPattern> <WhereClause?>> {case-insensitive}
  AsignSymbol.Pgql11AsignSymbol = <AS>  {case-insensitive}

  SelectOrUpdateClause = SelectClause
  SelectOrUpdateClause = UpdateClause

  SelectClause.SelectClause = <SELECT <Distinct?> <Projection>> {case-insensitive}
  Projection.ExpAsVars = <<{ExpAsVar ", "}+>>
  Projection.Star = <*>

  UpdateClause.UpdateClause = <UPDATE <{UpdateExp ", "}+>> {case-insensitive}
  UpdateExp.UpdateExp = <<PropRef> = <Exp>>
